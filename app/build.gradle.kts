import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.google.gms.google.services)
}

configurations.all {
    resolutionStrategy.force("org.jetbrains.kotlin:kotlin-metadata-jvm:2.4.0")
}

val configProps = Properties()
val configFile = rootProject.file("config.properties")

if (configFile.exists()) {
    configFile.inputStream().use {
        configProps.load(it)
    }
} else {
    throw GradleException(
        "config.properties not found: ${configFile.absolutePath}"
    )
}

val apiUrl = configProps.getProperty("API_URL")
    ?: throw GradleException("API_URL not found in config.properties")

val interstitialAdUnitId = configProps.getProperty("INTERSTITIAL_AD_UNIT_ID")
    ?: "demo-interstitial-yandex"

android {
    namespace = "com.mathgate.app"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.mathgate.app"
        minSdk = 25
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField(
            "String",
            "API_URL",
            "\"$apiUrl\""
        )
        buildConfigField(
            "String",
            "INTERSTITIAL_AD_UNIT_ID",
            "\"$interstitialAdUnitId\""
        )
    }

    testOptions {
        unitTests {
            all {
                it.maxHeapSize = "2048m"
            }
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildFeatures {
        buildConfig = true
        compose = true
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation("androidx.navigation:navigation-compose:2.9.8")
    implementation("androidx.datastore:datastore-preferences:1.2.1")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.10.0")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.room:room-ktx:2.8.4")
    implementation("androidx.room:room-runtime:2.8.4")
    implementation(libs.firebase.analytics)
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
    implementation(libs.androidx.hilt.navigation.compose)
    implementation("io.coil-kt.coil3:coil-svg:3.4.0")
    implementation("io.coil-kt.coil3:coil-compose:3.4.0")
    implementation("io.coil-kt.coil3:coil-network-okhttp:3.4.0")
    ksp("androidx.room:room-compiler:2.8.4")
    implementation("com.google.android.material:material:1.12.0")
    implementation("io.ktor:ktor-client-core:3.5.0")
    implementation("io.ktor:ktor-client-okhttp:3.5.0")
    implementation("io.ktor:ktor-client-content-negotiation:3.5.0")
    implementation("io.ktor:ktor-serialization-kotlinx-json:3.5.0")
    implementation("io.ktor:ktor-client-auth:3.5.0")
    implementation("io.ktor:ktor-client-logging:3.5.0")
    testImplementation("io.ktor:ktor-client-mock:3.5.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.11.0")
    implementation("com.yandex.android:mobileads:8.1.0")
    implementation(libs.latex.base)
    implementation(libs.latex.renderer)
    implementation(libs.latex.parser)
    implementation("org.jsoup:jsoup:1.22.2")
    testImplementation(libs.junit)
    testImplementation("io.mockk:mockk:1.14.11")
    testImplementation(libs.kotlinx.coroutines.test)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    debugImplementation(libs.androidx.compose.ui.tooling)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}
